<?xml version="1.0" encoding="utf-8"?>
<xsl:stylesheet version="1.0" xmlns:xsl="http://www.w3.org/1999/XSL/Transform">
	<xsl:output method="html" indent="yes" encoding="utf-8" />

	<xsl:template match="/*[local-name()='html']">
		<xsl:apply-templates select="*[local-name()='body']"/>
	</xsl:template>

	<xsl:template match="*[local-name()='body']">
		<strong><a name="toc">Table of contents</a></strong>
		<xsl:apply-templates select="*[local-name()='h2']|*[local-name()='h3']" mode="toc" />
		<xsl:apply-templates />
	</xsl:template>

	<xsl:template match="*[local-name()='h2']" mode="toc">
		<br /><a href="#h2_{generate-id(.)}"><xsl:value-of select="." /></a><br />
	</xsl:template>

	<xsl:template match="*[local-name()='h3']" mode="toc">
		<span style="font-size:small;">
			<xsl:text> - </xsl:text>
			<a href="#h3_{generate-id(.)}"><xsl:value-of select="." /></a><br />
		</span>
	</xsl:template>

	<xsl:template match="*[local-name()='h2']">
		<h2><a name="h2_{generate-id(.)}"><xsl:value-of select="." /></a><a href="#toc">up</a></h2>
	</xsl:template>

	<xsl:template match="*[local-name()='h3']">
		<h3><a name="h3_{generate-id(.)}"><xsl:value-of select="." /></a></h3>
	</xsl:template>


<!-- A default rule will just copy all the rest -->

	<xsl:template match="*">
		<xsl:element name="{name(.)}">
			<xsl:for-each select="@*">
				<xsl:attribute name="{name(.)}">
					<xsl:value-of select="." />
				</xsl:attribute>
			</xsl:for-each>
			<xsl:apply-templates />
		</xsl:element>
	</xsl:template>

</xsl:stylesheet>
